plugins {
    id 'java'
    id 'io.freefair.lombok' version '8.10.2'
    id 'org.springframework.boot' version '3.0.13'
    id 'io.spring.dependency-management' version '1.1.6'
    id 'jacoco'
    id 'org.sonarqube' version '5.1.0.4882'
    id 'maven-publish'
}

group = 'com.iexec.worker'

ext {
    springCloudVersion = '2022.0.5'
    commonsMathsVersion = '3.6.1'
    testContainersVersion = '1.20.4'
}

if (!project.hasProperty('gitBranch')) {
    ext.gitBranch = 'git rev-parse --abbrev-ref HEAD'.execute().text.trim()
}

if (gitBranch != 'main' && gitBranch != 'master' && !(gitBranch ==~ '(release|hotfix|support)/.*')) {
    version += '-NEXT-SNAPSHOT'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://docker-regis-adm.iex.ec/repository/maven-public/"
        credentials {
            username nexusUser
            password nexusPassword
        }
    }
    maven {
        url "https://jitpack.io"
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

dependencies {
    // iexec
    implementation "com.iexec.commons:iexec-commons-poco:$iexecCommonsPocoVersion"
    implementation "com.iexec.common:iexec-common:$iexecCommonVersion"
    implementation "com.iexec.commons:iexec-commons-containers:$iexecCommonsContainersVersion"
    implementation "com.iexec.result-proxy:iexec-result-proxy-library:$iexecResultVersion"
    implementation "com.iexec.sms:iexec-sms-library:$iexecSmsVersion"
    implementation "com.iexec.core:iexec-core-library:$iexecCoreVersion"

    // spring
    implementation "org.springframework.boot:spring-boot-starter"
    implementation "org.springframework.boot:spring-boot-starter-actuator"
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-websocket"
    implementation "org.springframework.cloud:spring-cloud-starter"
    implementation "org.springframework.cloud:spring-cloud-starter-openfeign"
    implementation "org.springframework.retry:spring-retry"

    // apache commons-lang3
    implementation 'org.apache.commons:commons-lang3'

    // Apache commons-math3
    implementation "org.apache.commons:commons-math3:${commonsMathsVersion}"

    // Required for com.iexec.worker.feign.config.RestTemplateConfig
    implementation 'org.apache.httpcomponents.client5:httpclient5'

    implementation 'org.glassfish.jersey.inject:jersey-hk2'
    implementation 'org.glassfish.jersey.bundles.repackaged:jersey-guava:2.25.1'

    // Removes 'warning: unknown enum constant When.MAYBE'
    implementation 'com.google.code.findbugs:annotations:3.0.1'

    // observability
    runtimeOnly 'io.micrometer:micrometer-registry-prometheus'

    // expiring map
    implementation 'net.jodah:expiringmap:0.5.10'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

springBoot {
    buildInfo()
}

tasks.named("bootJar") {
    manifest {
        attributes("Implementation-Title": "iExec Worker",
                "Implementation-Version": project.version)
    }
}

testing {
    suites {
        test {
            useJUnitJupiter()
            dependencies {
                implementation "org.springframework.boot:spring-boot-starter-test"
                implementation "org.mockito:mockito-inline" // activates mocking final classes/methods

                // awaitility
                implementation "org.awaitility:awaitility"

                // testcontainers
                implementation "org.testcontainers:junit-jupiter:$testContainersVersion"
            }
        }
    }
}

tasks.withType(Test).configureEach {
    finalizedBy jacocoTestReport
}

tasks.register('itest') {
    group 'Verification'
    description 'Runs the integration tests.'
}

// sonarqube code coverage requires jacoco XML report
jacocoTestReport {
    reports {
        xml.required = true
    }
}
tasks.sonarqube.dependsOn tasks.jacocoTestReport

publishing {
    publications {
        maven(MavenPublication) {
            artifact tasks.named("bootJar")
            from components.java
        }
    }
    repositories {
        maven {
            credentials {
                username nexusUser
                password nexusPassword
            }
            url project.hasProperty('nexusUrl') ? nexusUrl : ''
        }
    }
}

ext.jarPathForOCI = relativePath(tasks.bootJar.outputs.files.singleFile)
ext.gitShortCommit = 'git rev-parse --short=8 HEAD'.execute().text.trim()
ext.ociImageName = 'local/' + ['bash', '-c', 'basename $(git config --get remote.origin.url) .git'].execute().text.trim()

tasks.register('buildImage', Exec) {
    group 'Build'
    description 'Builds an OCI image from a Dockerfile.'
    dependsOn bootJar
    commandLine 'docker', 'build', '--build-arg', 'jar=' + jarPathForOCI, '-t', ociImageName + ':dev', '.'
}

// ##################
// #     proxy     #
// ##################

//gradle bootRun -PproxyHost=192.168.XX.XXX -PproxyPort=3128
project.ext.getJvmArgs = {
    if (project.hasProperty("proxyHost") && project.hasProperty("proxyPort")) {
        return ["-Dhttp.proxyHost=" + project.proxyHost, "-Dhttp.proxyPort=" + project.proxyPort]
    } else {
        return []
    }
}

bootRun {
    jvmArgs = project.ext.getJvmArgs()
}
